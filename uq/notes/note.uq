ENUM EnumNoteType (Text=0, Task=1);

-- type: 根据type，分配给特定的程序
TUID Note (
	id,
	main type SMALLINT DEFAULT 0,		-- Note类, EnumNoteType
	main caption CHAR(200),				-- 标题
	main content TEXT,					-- 内容，格式根据type而定，json
	main owner ID [$User],				-- 所有者
	main x TINYINT DEFAULT 0,			-- 删除标志
	main from ID [$User],				-- 派生来源人
	stamp (create, update) main,
);

TAG NoteTag;

MAP NoteTagMap (
	KEY note ID Note,
	KEY tag TAG NoteTag,
	INDEX tag_note(tag, note) unique,
);

-- Note 操作中附件的信息
MAP FLOW (
	KEY note ID Note,
	KEY flow SMALLINT DEFAULT 0,		-- 不断附加的id，跟操作相关
	operator ID [$User],				-- 操作人
	action TINYINT DEFAULT 0,			-- 由特别Note自定义
	stamp TIMESTAMP DEFAULT ONUPDATE,
	content TEXT,
);

-- ENUM EnumNoteFlag (None=0, Pin=1, Archive=2);

-- end 是不是可以 archive
-- state由特定程序定义，是json
MAP NoteX (
	KEY note ID Note,
	state TINYINT DEFAULT 0,				-- 由特定程序自己定义的特定的当前状态
	-- end TINYINT DEFAULT 0,				-- 1: end, can be archive
);

MAP FolderNote (
	KEY folder ID Folder,
	KEY note ID Note,
	type SMALLINT DEFAULT 0,			-- Note类, EnumNoteType
	seconds INT,						-- seconds since 2020-1-1
	unread TINYINT DEFAULT 0,			-- 1: read
	state TINYINT DEFAULT 0,			-- 对当前状态的描述，各type自行定义
	index folder_seconds(folder, seconds),
	index note_folder(note, folder) unique,
);

ENUM EnumNoteMapType (Spawn=0, Born=1, Contain=2)
MAP Note2Note (
	KEY note0 ID Note,
	KEY note1 ID Note,
	type TINYINT DEFAULT 0,				-- NoteMapType
);

MAP NoteAccess (
	KEY note ID Note,
	KEY user ID [$User],
	access TINYINT DEFAULT 0,				-- 1 表示允许
);

QUERY GetNote ver 1.0 (
	folder ID,						-- folderId < 0, 则是特定folder
	note ID,
)
RETURNS ret (
	id ID,
	type SMALLINT DEFAULT 0,		-- Note类, EnumNoteType
	caption CHAR(200),				-- 标题
	content TEXT,					-- 内容，格式根据type而定，json
	owner ID [$User],				-- 所有者
	x TINYINT DEFAULT 0,			-- 删除标志
	from ID [$User],				-- 派生来源人
	[$create] TIMESTAMP,
	[$update] TIMESTAMP,
	state TINYINT,	
)
RETURNS flow (
	flow SMALLINT,					-- 不断附加的id，跟操作相关
	operator ID,					-- 操作人
	action TINYINT,					-- 由特别Note自定义
	stamp TIMESTAMP,
	content TEXT,
) {
	IF folder < 0 {
		VAR fid ID;
		SET folder = a.folder FROM SpecFolder as a WHERE a.owner=$user AND a.spec=-folder;
	}
	BOOK FolderNote AT (folder, note) SET unread=0;
	INTO ret SELECT a.id, a.type, a.caption, a.content, a.owner, a.x, a.from, a.[$create], a.[$update], b.state
		FROM Note as a LEFT JOIN NoteX as b ON a.id=b.note
		WHERE a.id=note;
	INTO flow SELECT a.flow, a.operator, a.action, a.stamp, a.content
		FROM Flow as a
		WHERE a.note=note;
};

ACTION AddNote (
	caption CHAR(200),
	content TEXT,
	type SMALLINT,
)
RETURNS ret (
	note ID Note,
) {
	VAR noteId ID, folderId ID, seconds INT;
	TUID Note INTO noteId SET type=type, caption=caption, content=content, owner=$user;
	SET folderId=folder FROM SpecFolder WHERE owner=$user AND spec=EnumSpecFolder.notes;
	SET seconds=TIMESTAMPDIFF(second, '2020-1-1', $date);
	BOOK FolderNote AT (folderId, noteId) SET seconds=seconds, type=type;
	-- BOOK NoteAccess AT (noteId, $user) SET access=1; access不应该包括owner
	FOREACH (var noteFolderId ID OF SELECT folder as noteFolderId FROM FolderNote WHERE note=noteId) {
		IF noteFolderId<>folderId {
			BOOK FolderNote AT(noteFolderId, noteId) SET seconds=seconds, type=type;
		}
	}
	INTO ret SELECT noteId as note;
};

ACTION SetNote (
	note ID,
	caption CHAR(200),
	content TEXT,
) {
	VAR noteId ID, folderId ID, seconds INT;
	TUID Note ID note SET caption=caption, content=content;
	SET noteId = note;
	SET folderId=folder FROM SpecFolder WHERE owner=$user AND spec=EnumSpecFolder.notes;
	SET seconds=TIMESTAMPDIFF(second, '2020-1-1', $date);
	BOOK FolderNote AT(folderId, noteId) SET seconds=seconds; -- , type=type, sub=sub;
	FOREACH (var noteFolderId ID OF SELECT folder as noteFolderId FROM FolderNote WHERE note=noteId) {
		IF noteFolderId<>folderId {
			BOOK FolderNote AT(noteFolderId, noteId) SET seconds=seconds;
		}
	}
};

ACTION SendNoteTo (
	note ID,
	tos TEXT,
) {
	VAR seconds INT;
	SET seconds=TIMESTAMPDIFF(second, '2020-1-1', $date);
	TABLE tblTo(to ID);
	TEXT tos sep=',' ln='|' INTO tblTo;
	BOOK NoteAccess AT (note, *) SET access=0;
	FOREACH (var to ID OF SELECT to FROM tblTo) {
		VAR folderId ID;
		SET folderId = folder FROM SpecFolder WHERE owner=to AND spec=EnumSpecFolder.notes;
		IF folderId IS NULL {
			TUID Folder INTO folderId SET owner=to;
			BOOK SpecFolder AT(to, EnumSpecFolder.notes) SET folder=folderId;
		}
		BOOK FolderNote AT(folderId, note) SET seconds=seconds, unread=1;
		BOOK NoteAccess AT (note, to) SET access=1;
	};
};
