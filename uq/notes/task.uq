ENUM EnumTaskState (Start=0, Done=1, Pass=2, Fail=3, Rated=4, Canceled=5, Error=-1);
ENUM EnumTaskAction (Start=0, Done=1, Check=2, Rate=3);

MAP Task (
	KEY note ID Note,
	checker ID [$User],
	rater ID [$User],
	point INT,
);

ACTION AssignTask ver 1.01 (
	folder ID NOTE,
	note ID NOTE,
	caption CHAR(200),
	content TEXT,
	checker ID [$User],
	rater ID [$User],
	point INT,
	ARR tos (
		to ID [$User]
	),
) {
	VAR seconds INT, groupID INT;
	if folder IS NULL OR folder < 0 {
		SET groupID = -1;
	}
	ELSE {
		SET groupID = a.group FROM GroupFolder as a WHERE a.folder=folder AND a.member=$user;
		if groupID IS NULL {
			set groupID = -1;
		}
	}
	SET seconds=TIMESTAMPDIFF(second, '2020-1-1', $date);
	FOREACH tos {
		VAR taskId ID, folderId ID;
		SET folderId = NULL;
		if groupID >= 0 {
			SET folderId = folder FROM GroupFolder WHERE group=groupID AND member=to;
		}
		if folderId IS NULL {
			SET folderId = folder FROM SpecFolder WHERE owner=to AND spec=EnumSpecFolder.notes;
			IF folderId IS NULL {
				TUID Note INTO folderId SET owner=to, type=EnumNoteType.Folder;
				BOOK SpecFolder AT(to, EnumSpecFolder.notes) SET folder=folderId;
			}
		}
		TUID Note INTO taskId SET type=EnumNoteType.task, caption=caption, content=content, owner=to, from=$user;
		BOOK Task AT(taskId) SET checker=checker, rater=rater, point=point;
		BOOK NoteX AT(taskId) SET state=EnumTaskState.Start;
		BOOK FolderNote AT(folderId, taskId) SET seconds=seconds, unread=1;
		BOOK Note2Note AT(note, taskId) SET type=EnumNoteMapType.Spawn;
		BOOK Note2Note AT(taskId, note) SET type=EnumNoteMapType.Born;
		TUID [$User] ID (to) SET poke=1;
	};
};

ACTION DoneTask ver 1.01 (
	folder ID NOTE,
	note ID NOTE,
	content TEXT,		-- json
)
RETURNS ret (
	toState TINYINT
) {
	VAR seconds INT, groupID INT;
	SET seconds=TIMESTAMPDIFF(second, '2020-1-1', $date);

	VAR flowId SMALLINT, checker ID, rater ID, nextUser ID, point INT, folderId ID, parentId INT, state TINYINT;
	SET checker=a.checker, rater=a.rater, point=a.point, state=b.state
		FROM Task as a LEFT JOIN NoteX as b ON a.note=b.note
		WHERE a.note=note;
	IF state=EnumTaskState.Start {
		BOOK NoteX AT(note) SET state=EnumTaskState.Done, flowContent=content;
		SET flowId=(SELECT ifnull(max(a.flow), 0)+1 FROM Flow as a WHERE a.note=note);
		BOOK Flow AT(note, flowId) SET content=content, operator=$user, action=EnumTaskAction.Done;
		IF NOT checker IS NULL {
			SET nextUser= checker;
		}
		ELSEIF NOT rater IS NULL {
			SET nextUser = rater;
		}
		if NOT nextUser IS NULL {
			if folder IS NULL OR folder < 0 {
				SET groupID = -1;
			}
			ELSE {
				SET groupID = a.group FROM GroupFolder as a WHERE a.folder=folder AND a.member=$user;
				if groupID IS NULL {
					set groupID = -1;
				}
			}
			if groupID >= 0 {
				SET folderId = folder FROM GroupFolder WHERE group=groupID AND member=nextUser;
			}
			if folderId IS NULL {
				SET folderId = folder FROM SpecFolder WHERE owner=nextUser AND spec=EnumSpecFolder.notes;
				IF folderId IS NULL {
					TUID Note INTO folderId SET owner=nextUser, type=EnumNoteType.Folder;
					BOOK SpecFolder AT(nextUser, EnumSpecFolder.notes) SET folder=folderId;
				}
			}
			TUID [$User] ID (rater) SET poke=1;
		}
		IF NOT folderId IS NULL {
			BOOK FolderNote AT(folderId, note) SET seconds=seconds, unread=1;
		}
		SET parentId = note0 FROM Note2Note WHERE note1=note and type=EnumNoteMapType.Spawn;
		IF NOT parentId IS NULL {
			FOREACH (var parentFolderId ID, ownerId ID OF SELECT a.folder as parentFolderId, b.owner as ownerId 
					FROM FolderNote as a LEFT JOIN Note as b ON a.folder=b.id WHERE a.note=parentId) {
				BOOK FolderNote AT(parentFolderId, parentId) SET seconds=seconds;
				TUID [$User] ID (ownerId) SET poke=1;
			}
		}
		INTO ret SELECT EnumTaskState.Done as toState;
	}
	ELSE {
		INTO ret SELECT EnumTaskState.Error as toState;
	}
};

ACTION CheckTask ver 1.01 (
	folder ID NOTE,
	note ID Note,
	action TINYINT,		-- 1: pass，2: fail
	content TEXT,  		-- json
)
RETURNS ret (
	toState TINYINT
) {
	VAR seconds INT, groupID INT;
	SET seconds=TIMESTAMPDIFF(second, '2020-1-1', $date);

	VAR flowId SMALLINT, owner ID, checker ID, rater ID, point INT, folderId ID, state INT;
	SET checker=a.checker, rater=a.rater, point=a.point, state=b.state, owner=c.owner
		FROM Task as a LEFT JOIN NoteX as b ON a.note=b.note
		LEFT JOIN Note as c ON a.note=c.id
		WHERE a.note=note;
	IF state=EnumTaskState.Done AND $user=checker {
		IF action=1 {
			SET state=EnumTaskState.Pass;
		}
		else {
			SET state=EnumTaskState.Fail;
		}
		IF NOT checker IS NULL {
			BOOK NoteX AT(note) SET state=state, flowContent=content;
			SET flowId=(SELECT ifnull(max(a.flow), 0)+1 FROM Flow as a WHERE a.note=note);
			BOOK Flow AT(note, flowId) SET content=content, operator=$user, action=EnumTaskAction.Check;
			IF NOT rater IS NULL {
				if folder IS NULL OR folder < 0 {
					SET groupID = -1;
				}
				ELSE {
					SET groupID = a.group FROM GroupFolder as a WHERE a.folder=folder AND a.member=$user;
					if groupID IS NULL {
						set groupID = -1;
					}
				}
				if groupID >= 0 {
					SET folderId = folder FROM GroupFolder WHERE group=groupID AND member=rater;
				}
				if folderId IS NULL {
					SET folderId = folder FROM SpecFolder WHERE owner=rater AND spec=EnumSpecFolder.notes;
					IF folderId IS NULL {
						TUID Note INTO folderId SET owner=rater, type=EnumNoteType.Folder;
						BOOK SpecFolder AT(rater, EnumSpecFolder.notes) SET folder=folderId;
					}
				}
				TUID [$User] ID (rater) SET poke=1;

				SET folderId = folder FROM SpecFolder WHERE owner=rater AND spec=EnumSpecFolder.notes;
				BOOK FolderNote AT(folderId, note) SET seconds=seconds, unread=1;
				TUID [$User] ID (rater) SET poke=1;
			}
			TUID [$User] ID (owner) SET poke=1;
			-- Task owner Folder 需要更新
			-- SET folderId = folder FROM SpecFolder WHERE owner= AND spec=EnumSpecFolder.notes;
		}
		INTO ret SELECT state as toState;
	}
	ELSE {
		INTO ret SELECT EnumTaskState.Error as toState;
	}
};

ACTION RateTask ver 1.01 (
	folder ID NOTE,
	note ID Note,
	value INT,			-- 分值
	content TEXT,  		-- json
) {
	VAR seconds INT;
	SET seconds=TIMESTAMPDIFF(second, '2020-1-1', $date);

	VAR flowId SMALLINT, owner ID, checker ID, rater ID, point INT, folderId ID;
	SET checker=a.checker, rater=a.rater, point=a.point, owner=c.owner
		FROM Task as a LEFT JOIN Note as c ON a.note=c.id
		WHERE a.note=note;
	IF NOT checker IS NULL {
		TUID [$User] ID (checker) SET poke=1;
	}
	BOOK NoteX AT(note) SET state=EnumTaskState.Rated, flowContent=content;
	SET flowId=(SELECT ifnull(max(a.flow), 0)+1 FROM Flow as a WHERE a.note=note);
	BOOK Flow AT(note, flowId) SET content=content, operator=$user, action=EnumTaskAction.Rate;

	IF NOT rater IS NULL {
		if folder IS NULL OR folder < 0 {
			SET folderId = folder FROM SpecFolder WHERE owner=rater AND spec=EnumSpecFolder.notes;
			IF folderId IS NULL {
				TUID Note INTO folderId SET owner=rater, type=EnumNoteType.Folder;
				BOOK SpecFolder AT(rater, EnumSpecFolder.notes) SET folder=folderId;
			}
		}
		BOOK FolderNote AT(folderId, note) SET seconds=seconds, unread=1;
	}
	TUID [$User] ID (owner) SET poke=1;
	-- Task owner Folder 需要更新
	-- SET folderId = folder FROM SpecFolder WHERE owner= AND spec=EnumSpecFolder.notes;
	-- Task checker Folder 需要更新
	-- SET folderId = folder FROM SpecFolder WHERE owner= AND spec=EnumSpecFolder.notes;
};
